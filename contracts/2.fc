{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

(int) tlen (tuple t) asm "TLEN";

() recv_internal() {
}

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
  var output = empty_tuple();

  int m1 = matrixA.tlen(); ;; number of rows in MatrixA
  int n1 = matrixA.at(0).tlen(); ;; number of columns in MatrixA
  int m2 = matrixB.tlen(); ;; number of rows in MatrixB
  int n2 = matrixB.at(0).tlen(); ;; number of columns in MatrixB

  int i = 0;

  repeat (m1) {
    int j = 0;
    var row = empty_tuple();

    repeat (n2){
      int value = 0;
      int x = 0;
      repeat (m2){
        value = value + ((matrixA.at(i).at(x)) * (matrixB.at(x).at(j)));
        x = x + 1;
      }
      row~tpush(value);

      j = j + 1;
    }
    output~tpush(row);

    i = i + 1;
  }

  return output;
}
