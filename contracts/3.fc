{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

(int) ubitsize(int x) asm "UBITSIZE";
int slice_data_empty(slice s) asm "SDEMPTY";
int slice_refs_empty(slice s) asm "SREMPTY";

() recv_internal() {
}

(cell) loop_through_find(
        slice s, int x, builder b,

        int prev, int prev_size,
        int flag, int flag_size,
        int value, int value_size
) {
  while (1) {
    if (prev_size < flag_size) {
      int diff = min(flag_size - prev_size, slice_bits(s));
      prev_size += diff;
      prev <<= diff;
      prev += s~load_uint(diff);

      if (prev_size != flag_size) {
        if (slice_refs_empty(s)) {
          if (prev_size > 127 - x) {
            builder next = begin_cell();
            int magic = (prev_size - (127 - x));

            int v1 = prev >> magic;
            int v2 = prev - (v1 << magic);

            b~store_uint(v1, 127 - x);
            next~store_uint(v2, magic);

            return b.store_ref(next.end_cell()).end_cell();
          }

          b~store_uint(prev, prev_size);
          return b.end_cell();
        }

        s = s~load_ref().begin_parse();
      }
    } else {
      if (prev == flag) {
        prev_size = 0;
        prev = 0;

        if (value_size > 127 - x) {
          builder next = begin_cell();
          int magic = (value_size - (127 - x));

          int v1 = value >> magic;
          int v2 = value - (v1 << magic);

          b~store_uint(v1, 127 - x);
          next~store_uint(v2, magic);

          return b.store_ref(loop_through_find(
                  s, magic, next,
                  0, 0,
                  flag, flag_size,
                  value, value_size
          )).end_cell();
        } else {
          b~store_uint(value, value_size);
          x += value_size;
        }
      } else {
        ;; remove every left bits
        int lbits = prev >> (flag_size - 1);
        int size = prev_size - (flag_size - 1);

        b~store_uint(lbits, size);
        prev -= lbits << (flag_size - 1);

        x += size;
        prev_size -= size;

        if (x >= 127) {
          return b.store_ref(loop_through_find(
                  s, 0, begin_cell(),
                  prev, prev_size,
                  flag, flag_size,
                  value, value_size
          )).end_cell();
        }
      }
    }
  }

  return b.end_cell();
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {

  int flag_size = ubitsize(flag);
  int value_size = ubitsize(value);

  return loop_through_find(linked_list.begin_parse(), 0, begin_cell(),
          0, 0,
          flag, flag_size,
          value, value_size);
}